%% A more complicated example of frames

person ako [] with
[
	name:
	[
		range		atom,
		help		respond << The name should be a string. >> ,
		if_needed	ask_user("What is this person's name?"),
		if_new		query(ask_name, [init]),
		cache		true
	],
	sex:
	[
		range		[male, female],
		help		respond << Sex can only be male or female, not ^new_value >>,
		if_needed	ask_user("What is the person's gender?"),
		if_replaced	print("Are you sure you want a sex change?"),
		if_removed	print("Are you sure you want the sex removed?"),
		cache		true
	],
	year_of_birth:
	[
		range		year(current_date) - 120 .. year(current_date),
		help		print("Invalid year of birth."),
		if_needed	ask_user("Year of birth"),
		cache		true
	],
	age:
	[
		cache		true,
		if_needed	year(current_date) - year_of_birth
	],
	parents:
	[
		multivalued	true,
		range		person,
		help		print("The value in a parents slot must be a person.")
	],
	height:
	[
		range		10..220,
		cache		true,
		help
		(		if new_value < 10 then
					print(new_value, "cm is too short."),
				if new_value > 220 then
					print(new_value, "cm is too tall."),
				print("The height should be between 10 and 220cm.")
		),
		if_needed	ask_user("What is the height of ", name)
	],
	weight:
	[
		range		1..150,
		help		print("Weight should be in the range 1 .. 150"),
		if_needed	ask_user("What is the weight of ", name),
		cache		true,
		if_added	if new_value > 100 then
					print("Your ", current_slot, " is too high!")
	],
	occupation:
	[
		range		atom,
		help		print("The occupation should be a string."),
		if_needed	ask_user("What is the occupation"),
		cache		true,
		if_removed	print("I used to be a ", old_value, ".")
	]
]!


measure ako [] with
[
	current_value:
	[
		range		allowable_low .. allowable_high,
		help		respond << That value is out of range! Please try again. >> ,
		if_new		query(ask_measure, [prompt]),
		cache		true,
		if_added:	if	new_value < expected_low then
					interpretation is_replaced_by low
				else if	new_value > expected_high then
					interpretation is_replaced_by high
				else	interpretation is_replaced_by normal,
		if_replaced	last_value is_replaced_by old_value
	],
	interpretation:
	[
		range		[low, normal, high],
		help		print("Interpretation can only be 'low', 'normal' or 'high'")
	]
]!

%% Should this be a frame or generic???

ph_frame ako measure with
[
	prompt:		[if_needed	pH_level],
	allowable_low:	[if_needed	6],
	allowable_high:	[if_needed	8],
	expected_high:	[if_needed	7.6],
	expected_low:	[if_needed	6.5]
]!

'HCO3_frame' ako measure with
[
	prompt:		[if_needed	"HCO3_level"],
	allowable_low:	[if_needed	6],
	allowable_high:	[if_needed	8],
	expected_high:	[if_needed	7.6],
	expected_low:	[if_needed	6.5]
]!

paCO2_frame ako measure with
[
	prompt:		[if_needed	paCO2_level],
	allowable_low:	[if_needed	6],
	allowable_high:	[if_needed	8],
	expected_high:	[if_needed	7.6],
	expected_low:	[if_needed	6.5]
]!

patient ako person with
[
	ph:
	[
		cache		true,
		if_needed	new [ph_frame]
	],
	'HCO3':
	[
		cache		true,
		if_needed	new ['HCO3_frame']
	],
	paCO2:
	[
		cache		true,
		if_needed	new [paCO2_frame]
	],
	diagnosis:
	[
		multivalued	true,
		default		[]
	],
	investigation:
	[
		if_new
		(		if	interpretation(ph) = low
				then	diagnosis has_value acidosis,

				if	interpretation(ph) = high
				then	diagnosis has_value alkalosis,

				if	interpretation(paCO2) = low
				then	diagnosis has_value hypocarbic,

				if	interpretation(paCO2) = high
				then 	diagnosis has_value hypercarbic,

				if 	acidosis in diagnosis
				and	interpretation('HCO3') = low
				then	diagnosis has_value primary_metabolic_acidosis
		)
	]
]!

current_date isa [] with
[
	year:	2002,
	month:	5,
	day:	5
]!

%fred isa patient with [name: fred, occupation: lecturer]!
%jim isa person with [name: jim]!

script('../scripts/patient.script')!

go :- fred isa patient with [].
